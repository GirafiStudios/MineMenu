plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

version = "${version}${buildnumber}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
	
    modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forge_config_api_port}"
	modApi ("mysticdrew:common-networking-fabric:${common_networking}-${minecraft_version}") { transitive false }
    include ("mysticdrew:common-networking-fabric:${common_networking}-${minecraft_version}") { transitive false }
    modImplementation "com.blamejared.controlling:Controlling-fabric-${minecraft_version}:${controlling}"
    modImplementation "com.blamejared.searchables:Searchables-fabric-${minecraft_version}:${searchables}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}
