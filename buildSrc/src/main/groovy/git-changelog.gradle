/* Originally made by DarkhaxDev */

project.ext.mod_changelog = providers.provider {
    'No changelog was provided. Please refer to the project page for more information.'
}

try {
    def gitCommit = providers.environmentVariable("GIT_COMMIT").orElse(
            getExecOutput(["git", "log", "-n", "1", "--pretty=tformat:%h"])
        )

    def gitPrevCommit = providers.environmentVariable("GIT_PREVIOUS_COMMIT")

    // If a full range is available use that range.
    project.ext.mod_changelog = gitPrevCommit.flatMap { prev ->
        gitCommit.flatMap { commit ->
            if (prev && commit) {
                getExecOutput(["git", "log", "--pretty=tformat:- %s", "${prev}..${commit}"])
                    .map {
                        project.logger.lifecycle("Generated changelog using commits ${prev} to ${commit}.")
                        it
                    }
            } else {
                providers.provider { null }
            }
        }
    }.orElse(
        // If only one commit is available
        gitCommit.flatMap { commit ->
            getExecOutput(["git", "log", "--pretty=tformat:- %s", "-1", commit])
                .map {
                    project.logger.lifecycle("Generated changelog using commit ${commit}.")
                    it
                }
        }
    )
}

catch (Exception e) {
    project.logger.warn("Changelogs could not be generated! ${e.message}")
}

def getExecOutput(List<String> commands) {
    return providers.exec {commandLine commands}.standardOutput.asText.map { it.trim() }
}
